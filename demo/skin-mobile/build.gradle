apply plugin: 'com.android.application'

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "skin.support.mobile.demo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        buildConfigField("String", "BUILD_TIME", "\"" + new Date().format("yyyy-MM-dd") + "\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res',
                           'src/main/res-black',
                           'src/main/res-blue',
                           'src/main/res-green',
                           'src/main/res-orange',
                           'src/main/res-pink',
                           'src/main/res-red',
                           'src/main/res-white',
                           'src/main/res-yellow']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.cardview:cardview:$cardViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintVersion"
    implementation "com.google.android.material:material:$materialVersion"

    if (isUseLocal) {
        implementation project(':androidx:skin-support')
        implementation project(':androidx:skin-support-appcompat')
        implementation project(':androidx:skin-support-design')
        implementation project(':androidx:skin-support-constraint-layout')
        implementation project(':androidx:skin-support-cardview')
    } else {
        // skin-support
        implementation "io.github.boiqin:skin-support:$skinAndroidXVersion"
        // skin-support 基础控件支持
        implementation "io.github.boiqin:skin-support-appcompat:$skinAndroidXVersion"
        // skin-support-design material design 控件支持[可选]
        implementation "io.github.boiqin:skin-support-material:$skinAndroidXVersion"
        // skin-support-cardview CardView 控件支持[可选]
        implementation "io.github.boiqin:skin-support-cardview:$skinAndroidXVersion"
        // skin-support-constraint-layout ConstraintLayout 控件支持[可选]
        implementation "io.github.boiqin:skin-support-constraintlayout:$skinAndroidXVersion"
    }
}
